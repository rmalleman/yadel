group 'org.roylance.yadel'
version "0.$actualVersion-SNAPSHOT"

buildscript {
    ext.kotlinVersion = "1.0.3"
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'http://chaperapp.dyndns-server.com:8081/artifactory/libs-snapshot-local' }
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:3.1.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.roylance.yaclib:core:0.53-SNAPSHOT"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url 'http://chaperapp.dyndns-server.com:8081/artifactory/libs-snapshot-local' }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

artifactory {
    contextUrl = 'http://chaperapp.dyndns-server.com:8081/artifactory'
    publish {
        repository {
            repoKey = 'libs-snapshot-local'
            username = System.getenv('ARTIFACTORY_USER')
            password = System.getenv('ARTIFACTORY_PASSWORD')
        }

        defaults {
            publications('mavenJava')
            publishBuildInfo = true
            publishArtifacts = true
            properties = ['qa.level': 'basic', 'dev.team' : 'core']
            publishPom = true
        }
    }
    resolve {
        repository {
            repoKey = 'repo'
        }
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    compile 'com.typesafe.akka:akka-cluster_2.11:2.4.7'
    compile 'commons-io:commons-io:2.5'
    compile 'org.roylance:common:0.4-SNAPSHOT'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task runYaclib(type: GradleBuild) {
    buildFile = 'yaclib.gradle'
    tasks = ['execute']
}
